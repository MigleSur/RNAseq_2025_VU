import os

# List your sample names (without file extension)
samples = ["SRR15618897", "SRR15618896"]

input_path = "/home/bda/migle/raw_data_exercise_20250220"
 
genome_index_dir = "/home/bda/migle/raw_data_exercise_20250220/mouse_reference" 

genes_bed = "data/annotation/genes.bed" # update the path

stages = ["raw", "filtered"]

# thread_no = 2

#######
# ALL #
#######

# The 'all' rule defines the final targets of your pipeline
rule all:
    input:
        expand("results/fastqc/{sample}_{stage}_fastqc.html", 
            sample=samples,
            stage=stages),
        expand("results/fastp/{sample}_fastp.html", sample=samples),
        expand("results/hisat2/{sample}.bam", sample=samples),
        expand("results/hisat2/{sample}.sorted.bam", sample=samples),
        expand("results/hisat2/{sample}.sorted.bam.bai", sample=samples),

##########
# FastQC #
##########

rule fastqc:
    input:
        expand("{path}/{{sample}}_{{stage}}.fastq.gz", path=input_path)
    output:
        html = "results/fastqc/{sample}_{stage}_fastqc.html",
        zip  = "results/fastqc/{sample}_{stage}_fastqc.zip"
    conda: "envs/preprocess_rnaseq.yaml"
    shell:
        """
            fastqc {input} --outdir results/fastqc
        """

#########
# Fastp #
#########

rule fastp:
    input: 
        expand("{path}/{{sample}}_raw.fastq.gz", path=input_path)
    output:
        filtered = expand("{path}/{{sample}}_filtered.fastq", path=input_path),
        html = "results/fastp/{sample}_fastp.html",
        json = "results/fastp/{sample}_fastp.json"
    conda: "envs/preprocess_rnaseq.yaml"
    params:
        quality = 25,
    shell:
        """
        fastp -i {input} \
        -q {params.quality} \
        --trim_poly_g \
        --trim_poly_x \
        -o {output.filtered} \
        -h {output.html} \
        -j {output.json}
        """

##########
# HISAT2 #
##########

rule hisat2:
    input:
        fastq=expand("{path}/{{sample}}_filtered.fastq", path=input_path)
    output:
        bam="results/hisat2/{sample}.bam"
    params:
        index=genome_index_dir 
    conda: "envs/preprocess_rnaseq.yaml"
    threads: 2
    shell:
        """
        hisat2 -p {threads} -x {params.index} -U {input.fastq} |
        samtools view -Sb - > {output.bam}
        """

rule sort_bam:
    input:
        unsorted_bam="results/hisat2/{sample}.bam"
    output:
        sorted_bam="results/hisat2/{sample}.sorted.bam"
    conda: "envs/preprocess_rnaseq.yaml"
    threads: 4
    shell:
        """
            samtools sort -@ {threads} -o {output.sorted_bam} {input.unsorted_bam}
        """
    
rule index_bam:
    input:
        sorted_bam="results/hisat2/{sample}.sorted.bam"
    output:
        bam_index="results/hisat2/{sample}.sorted.bam.bai"
    conda: "envs/preprocess_rnaseq.yaml"
    shell:
        """
            samtools index {input.sorted_bam}
        """

#########
# RSeQC #
#########

rule rseqc_gene_body_coverage:
    input:
        sorted_bam="results/hisat2/{sample}.sorted.bam",
        bed=genes_bed 
    output:
        txt="results/rseqc/{sample}.geneBody_coverage.txt",
        pdf="results/rseqc/{sample}.geneBody_coverage.pdf"
    conda: "envs/preprocess_rnaseq.yaml"
    params:
        prefix="results/rseqc/{sample}.geneBody_coverage"
    threads: 2
    shell:
        """
        geneBody_coverage.py -r {input.bed} -i {input.sorted_bam} -o {params.prefix}
        """


###########
# MULTIQC #
###########

rule test_multiqc_file:
    input:
        expand("samtools_stats/{sample}.txt", sample=["a"]),
    output:
        "results/multiqc/multiqc.html",
        "results/multiqc/multiqc.data.zip",
    params:
        extra="--verbose",  # Optional: extra parameters for multiqc.
        use_input_files_only=True,  # Optional, use only a.txt and don't search folder samtools_stats for files
    log:
        "logs/multiqc.log",
    wrapper:
        "v5.8.0/bio/multiqc"
